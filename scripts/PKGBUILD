# Maintainer: Nick Kavalieris <ph4ntome[at]gmail[dot]com>
# Upstream Author : George Tzoumas <geotz[at]gmail[dot]com>
# For contributors check the AUTHORS file

# This file is also included in the source tree for purposes of completeness
# The normal way of aqusition is from AUR: https://aur.archlinux.org/packages/callirhoe/

pkgname=callirhoe
pkgver=228
pkgrel=1
pkgdesc="PDF & Photo calendar creator with high quality vector graphics"
url="https://code.google.com/p/callirhoe/"
arch=('any')
license=('GPLv3')
depends=('python2' 'imagemagick' 'python2-cairo' 'subversion')
source=("$pkgname::svn+https://callirhoe.googlecode.com/svn/trunk")
md5sums=('SKIP')


pkgver() {
    cd "$pkgname"
    svnversion | tr -d [A-z] | sed 's/ *//g'
}

build() {
    cd "${srcdir}/${pkgname}"
    make
}

package() {
    cd "${srcdir}/${pkgname}"
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
    make DESTDIR="$pkgdir/usr" install
}